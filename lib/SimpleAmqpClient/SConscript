#
# Copyright (c) 2017 Juniper Networks, Inc. All rights reserved.
#

import subprocess

env = DefaultEnvironment()

vpath = '#third_party/SimpleAmqpClient'
working_dir = Dir('.').abspath
products = [File('#build/lib/' + env['LIBPREFIX'] + 'SimpleAmqpClient' + env['LIBSUFFIX'])]

librabbitmq = 'librabbitmq.a'
shared_libs_mode = 'OFF'

cmake_flags = [
    '-DCMAKE_INSTALL_PREFIX=' + Dir('#build').abspath,
    '-DRabbitmqc_INCLUDE_DIR=' + Dir('#build/include').abspath,
    '-DRabbitmqc_LIBRARY=' + File('#build/lib/' + librabbitmq).abspath,
    '-DENABLE_SSL_SUPPORT=ON',
    '-DBUILD_SHARED_LIBS=' + shared_libs_mode,
]

# Header files cannot be listed in some smart way (e.g. using glob())
# because only part of headers from third_party are installed with cmake.
header_files = [
    'AmqpException.h',
    'AmqpResponseLibraryException.h',
    'BadUriException.h',
    'BasicMessage.h',
    'Channel.h',
    'ConnectionClosedException.h',
    'ConsumerCancelledException.h',
    'ConsumerTagNotFoundException.h',
    'Envelope.h',
    'MessageReturnedException.h',
    'SimpleAmqpClient.h',
    'Table.h',
    'Util.h',
    'Version.h',
]

header_files = [File('#build/include/SimpleAmqpClient/' + h).abspath for h in header_files]

def build_simple_amqp_client(target, source, env):
    env.EnsureBuildDependency('cmake')

    subprocess.call(['cmake'] + cmake_flags + [Dir(vpath).abspath], cwd=working_dir)
    subprocess.call(['cmake', '--build', '.', '--target', 'install'], cwd=working_dir)

libSimpleAmqpClient = env.Command(products + header_files, Dir(vpath), build_simple_amqp_client)

env.Requires(libSimpleAmqpClient, '#build/lib/' + librabbitmq)
