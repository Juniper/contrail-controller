#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

#-*- mode: pythos; -*-

import sys
Import('AgentEnv')
env = AgentEnv.Clone()

if sys.platform == 'darwin':
    Return()

SandeshGenFiles = env.SandeshGenCpp('agent_ksync.sandesh')
SandeshGenFiles += env.SandeshGenCpp('../../../vrouter/sandesh/vr.sandesh')
SandeshGenSrcs = env.ExtractCpp(SandeshGenFiles)

except_env = AgentEnv.Clone()
cflags = except_env['CCFLAGS']
if '-fno-exceptions' in cflags:
    cflags.remove('-fno-exceptions')
    except_env.Replace(CCFLAGS = cflags)

sandesh_objs = []
for src in SandeshGenSrcs:                    
    objname = src.replace('.cpp', '.o')
    obj = except_env.Object(objname, src)
    sandesh_objs.append(obj)

except_files = [
                'vnswif_listener.cc'
                ]

except_objs = []
for except_src in except_files:
    objname = except_src.replace('.cc', '.o')
    obj = except_env.Object(objname, except_src)
    except_objs.append(obj)

libksync = env.Library('vnswksync',
                       sandesh_objs +
                       except_objs +
                       ['ksync_init.cc',
                        'interface_ksync.cc',
                        'interface_scan.cc',
                        'mirror_ksync.cc',
                        'mpls_ksync.cc',
                        'nexthop_ksync.cc',
                        'route_ksync.cc',
                        'flowtable_ksync.cc',
                        'vxlan_ksync.cc',
                        'sandesh_ksync.cc',
                        'vrf_assign_ksync.cc'
                       ])

env.SConscript('test/SConscript', exports='AgentEnv', duplicate = 0)
