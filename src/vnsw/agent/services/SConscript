#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

# -*- mode: python; -*-
# services directory

Import('AgentEnv')
env = AgentEnv.Clone()

env.Depends('metadata_proxy.o', '#build/include/isc')

import sys
if sys.platform != 'darwin':
    SandeshGenFiles = env.SandeshGenCpp('services.sandesh')
    SandeshGenSrcs = env.ExtractCpp(SandeshGenFiles)

    except_env = AgentEnv.Clone()
    cflags = except_env['CCFLAGS']
    if '-fno-exceptions' in cflags:
        cflags.remove('-fno-exceptions')
        except_env.Replace(CCFLAGS = cflags)

    sandesh_objs = []
    for src in SandeshGenSrcs:                    
        objname = src.replace('.cpp', '.o')
        obj = except_env.Object(objname, src)
        sandesh_objs.append(obj)

    libservices = env.Library('agent_services',
                     sandesh_objs +
                     [
                      'arp_proto.cc',
                      'arp_handler.cc',
                      'dhcp_proto.cc',
                      'dhcp_handler.cc',
                      'dns_proto.cc',
                      'dns_handler.cc',
                      'icmp_proto.cc',
                      'icmp_handler.cc',
                      'metadata_proxy.cc',
                      'services_init.cc',
                      'services_sandesh.cc',
                      ])

env.SConscript('test/SConscript', exports='AgentEnv', duplicate = 0)
