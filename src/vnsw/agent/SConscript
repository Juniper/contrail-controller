#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

import re
import time
import subprocess

Import('BuildEnv')
env = BuildEnv.Clone()

import sys
if sys.platform == 'darwin':
    Return()

def MapBuildDir(list):
    return map(lambda x: Dir('../../' + x).abspath, list)

env.Prepend(CPPPATH = '#/vrouter/include')
env.Prepend(CPPPATH = env['TOP'] + '/base/sandesh')
env.Prepend(CPPPATH = env['TOP'] + '/discovery/client')
env.Prepend(CPPPATH = env['TOP'] + '/dns')
env.Prepend(CPPPATH = env['TOP'] + '/http/client')
env.Prepend(CPPPATH = env['TOP'] + '/ifmap')
env.Prepend(CPPPATH = env['TOP'] + '/schema')
env.Prepend(CPPPATH = env['TOP'] + '/sandesh')
env.Prepend(CPPPATH = env['TOP'] + '/vnsw/agent')
env.Prepend(CPPPATH = env['TOP'] + '/vnsw/agent/cmn')
env.Prepend(CPPPATH = env['TOP'] + '/vnsw/agent/init')
env.Prepend(CPPPATH = env['TOP'] + '/vnsw/agent/openstack')
env.Prepend(CPPPATH = env['TOP'] + '/vnsw/agent/oper')
env.Prepend(CPPPATH = env['TOP'] + '/vnsw/agent/uve')
env.Prepend(CPPPATH = env['TOP'] + '/vrouter/sandesh')
env.Prepend(CPPPATH = env['TOP'])
env.Prepend(CPPPATH = env['TOP_INCLUDE'] + '/thrift')

env.Prepend(
    LIBS=[
        'nova_ins',
        'nova_ins_thrift',
        'xmpp',
        'peer_sandesh',
        'xmpp_multicast',
        'xmpp_enet',
        'xmpp_unicast',
        'isc',
        'xml2',
        'ds',
        'xml',
        'pugixml',
        'curl',
        'bind_interface',
        'bgp_schema',
        'ifmap_vnc',
        'ifmap_agent',
        'ifmap_common', 
        'kstate',
        'net',
        'sandeshflow',
        'sandeshvns',
        'filter',
        'vgw',
        'httpc',
        'route',
        'db',
        'cpuinfo',
        'io',
        'sandesh',
        'http',
        'http_parser',
        'base',
        'thriftasio',
        'thrift',
        'boost_filesystem',
        'boost_program_options',
        'boost_regex',
])

if sys.platform != 'darwin':
    env.Prepend(LIBS=[
        'agent_services',
        'diag',
        'vnswksync',
        'ksync',
        'vrutil',
        'agentuve',
        ])

env.Prepend(LIBS=['vnswoperdb', 'vnswctrl', 'vnswcmn', 'vnswinit', 'cfg', 'filter'])

if sys.platform == 'darwin':
    kstate_lib =  Dir('./kstate').path + '/libkstate.a'
    env.Prepend(LINKFLAGS =
                ['-Wl,-force_load,' + kstate_lib])
else:
    env.Prepend(LINKFLAGS =
                ['-Wl,--whole-archive',
                 '-rdynamic',
                 '-lkstate',
                 '-lcpuinfo',
                 '-lvnswoperdb',
                 '-Wl,--no-whole-archive'])

if sys.platform != 'darwin':
    env.Append(LIBS=['pkt', 'rt'])

libpath = MapBuildDir(
    [
     'base',
     'bgp',
     'db',
     'discovery/client',
     'dns/bind',
     'http/client',
     'ifmap',
     'io',
     'ksync',
     'net',
     'route',
     'schema',
     'vnsw/agent',
     'vnsw/agent/cfg',
     'vnsw/agent/cmn',
     'vnsw/agent/controller',
     'vnsw/agent/diag',
     'vnsw/agent/filter',
     'vnsw/agent/init',
     'vnsw/agent/kstate', 
     'vnsw/agent/ksync',
     'vnsw/agent/openstack',
     'vnsw/agent/oper',
     'vnsw/agent/pkt',
     'vnsw/agent/services',
     'vnsw/agent/uve',
     'vnsw/agent/vgw',
     'vrouter/utils',
     'xml',
     'xmpp',
     'third_party/bind-9.9.2-P1/lib/isc',
])

env.Append(LIBPATH=libpath)

buildinfo_dep_libs = [
    '#build/lib/libthrift.a',
    '#build/lib/libthriftasio.a',
    '#build/lib/libboost_program_options.a',
    '#build/lib/libboost_regex.a',
    '#build/lib/libboost_system.a', 
    '#build/lib/libcurl.a',
    '#build/lib/libhttp.a',
    '#build/lib/libhttp_parser.a', 
    '#build/lib/liblog4cplus.a',
    '#build/lib/libpugixml.a',
    '../../../lib/libsandesh.a',
    '../../../lib/libsandeshflow.a', 
    '../../../lib/libsandeshvns.a',
    '../../base/libbase.a',
    '../../base/libcpuinfo.a',
    '../../db/libdb.a',
    '../../dns/bind/libbind_interface.a',
    '../../ifmap/libifmap_agent.a',
    '../../ifmap/libifmap_common.a', 
    '../../io/libio.a', 
    '../../ksync/libksync.a',
    '../../net/libnet.a',
    '../../route/libroute.a', 
    '../../schema/libbgp_schema.a',
    '../../schema/libifmap_vnc.a',
    '../../schema/libxmpp_unicast.a',
    '../../schema/libxmpp_multicast.a',
    '../../vrouter/utils/libvrutil.a',
    '../../xml/libxml.a',
    '../../xmpp/libxmpp.a', 
    'cfg/libcfg.a',
    'cmn/libvnswcmn.a',
    'controller/libvnswctrl.a',
    'filter/libfilter.a',
    'kstate/libkstate.a',
    'ksync/libvnswksync.a',
    'openstack/libnova_ins.a',
    'openstack/libnova_ins_thrift.a', 
    'oper/libvnswoperdb.a',
    'pkt/libpkt.a', 'diag/libdiag.a', 
    'services/libagent_services.a',
    'uve/libagentuve.a',
    ]

if sys.platform == 'linux':
    buildinfo_dep_libs += [ '../../../lib/libtbb_debug.so.2' ]

env.GenerateBuildInfoCode(
    target='buildinfo.cc',
    source = buildinfo_dep_libs + ['main.cc'],
    path=str(Dir('.').abspath))

#Add -Wno-return-type to ignore errors from thrift generated code in 
#InstanceService.h
cflags = env['CCFLAGS']
cflags.append('-Wno-return-type')
vnswad = env.Program(target = 'vnswad', source = ['buildinfo.cc', 'main.cc'])
env.Alias('src/vnsw/agent:vnswad', vnswad)
env.Default(vnswad)
env.Alias('install', env.Install(env['INSTALL_BIN'], vnswad))

AgentEnv = env.Clone()
AgentEnv.AppendUnique(CCFLAGS='-fno-exceptions')
cflags = AgentEnv['CCFLAGS']
cflags.remove('-Wno-return-type')
AgentEnv.Replace(CCFLAGS = cflags)
Export('AgentEnv')

# TODO(md): temporarily removed test subdirectory.
subdirs = ['cmn',
           'cfg',
           'controller',
           'diag',
           'filter',
           'init',
           'kstate',
           'ksync',
           'openstack',
           'oper',
           'pkt',
           'services',
           'uve',
           'vgw'
           ]

for dir in subdirs:
    env.SConscript(dir + '/SConscript', exports=['AgentEnv', 'BuildEnv'], duplicate=0)


def valgrind_run(target, source, env):
    #import pdb; pdb.set_trace()

    #execute tests with valgrind
    import subprocess
    ShEnv = {env['ENV_SHLIB_PATH']: 'build/lib'}

    import os
    # Get test alias from agent-test test suite
    for test in env.ans['agent-test'].sources:
        cmd = test.sources[0].path
        logfile = open(cmd + '.log', 'w')
        valgrindfile = '--log-file=' + cmd + '.valgrind'
        supp_file = '--suppressions=' + 'src/vnsw/agent/test/valgrind.supp'
        print cmd + ' logfile=' + cmd + '.log' + ' valgrind_file=' + cmd + '.valgrind '
        code = subprocess.call(['valgrind', '-v', '--track-origins=yes',
                                '--leak-check=full', '--error-exitcode=1',
                                '--show-reachable=yes', supp_file, valgrindfile,
                                cmd], stdout=logfile, stderr=logfile, env=ShEnv)
        if code == 0:
            print cmd + '\033[94m' + " PASS" + '\033[0m'
        else:
            if code < 0:
                logfile.write('Terminated by signal: ' + str(-code) + '\n')
            print cmd + '\033[91m' + " FAIL" + '\033[0m'

#test_valgrind = env.Command(Dir('test_valgrind'), '', valgrind_run)
#env.Alias('src/vnsw/agent:test_valgrind', test_valgrind)

def code_coverage(target, source, env):
    # rm -rf vnsw/agent/test_coverage
    import shutil
    #import pdb; pdb.set_trace()
    shutil.rmtree(target[0].path, ignore_errors = True)

    # lcov --base-directory $ROOT -- directory . --zerocounters -q
    import os
    os.system('lcov --base-directory . --directory ' + Dir('.').path +
              ' --zerocounters -q')
    # execute tests
    import subprocess
    #import pdb; pdb.set_trace()
    ShEnv = {env['ENV_SHLIB_PATH']: 'build/lib'}

    # Get test alias from agent-test test suite 
    for test in env.ans['agent-test'].sources:
        cmd = test.sources[0].path
        print cmd
        logfile = open(cmd + '.log', 'w')
        subprocess.call([cmd], stdout=logfile, stderr=logfile, env=ShEnv)

    # lcov --base-directory $ROOT -- directory . -c -o bgp_test.info
    os.system('lcov --base-directory . --directory ' + Dir('.').path +
              ' -c --ignore-errors gcov -o agent_test.info')

    # genhtml -o bgp/test_coverage bgp_test.info
    os.system('genhtml -o ' + target[0].path +
              ' -t "test coverage" --num-spaces 4 agent_test.info')


#.de.byte.breaker disable tests
if False:
#if env['OPT'] == 'coverage':
    #import pdb; pdb.set_trace()
    test_coverage = env.Command(Dir('test_coverage'), '', code_coverage)
    env.AlwaysBuild(test_coverage)
    env.Alias('src/vnsw/agent:test_coverage', test_coverage)

def run_ut(target, source, env):
    # execute tests
    import subprocess
    #import pdb; pdb.set_trace()
    ShEnv = {env['ENV_SHLIB_PATH']: 'build/lib'}

    # Get test alias from agent-test test suite 
    for test in env.ans['agent-test'].sources:
        cmd = test.sources[0].path
        log = test.sources[0].path + '.log'
        logfile = open(cmd + '.log', 'w')
        proc = subprocess.Popen(cmd, stdout=logfile, stderr=logfile, env=ShEnv)

        # 60 second timeout
        timeout = 60
        for i in range(timeout):
            code = proc.poll()
            if not code is None:
                break
            time.sleep(1)

        if code is None:
            proc.kill()
            logfile.write('[  TIMEOUT  ] ')
            print cmd + '\033[91m' + " TIMEOUT" + '\033[0m'
            return

        if code == 0:
            print cmd + '\033[94m' + " PASS" + '\033[0m'
        else:
            logfile.write('[  FAILED  ] ')
            if code < 0:
                logfile.write('Terminated by signal: ' + str(-code) + '\n')
            else:
                logfile.write('Program returned ' + str(code) + '\n')
            print cmd + '\033[91m' + " FAIL" + '\033[0m'



#run_ut = env.Command(Dir('run_ut'), '', run_ut)
#env.AlwaysBuild(run_ut)
#env.Alias('src/vnsw/agent:run_ut', run_ut)
env.Depends('main.o', 'buildinfo.cc')

# Local Variables:
# mode: python
# End:
