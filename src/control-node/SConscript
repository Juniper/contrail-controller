#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

# -*- mode: python; -*-

Import('BuildEnv')
import sys

def MapBuildDir(list):
    return map(lambda x: '#/' + Dir('.').path + '/../' + x, list)

env = BuildEnv.Clone()

libcontrol_node = env.Library('control_node', ['control_node.cc'])

env.Append(CPPPATH = [env['TOP'] + '/base/sandesh'])
env.Append(CPPPATH = [env['TOP'] + '/ifmap'])
env.Append(CPPPATH = [env['TOP'] + '/bgp'])
ControlNodeSandeshGenFiles = env.SandeshGenCpp('sandesh/control_node.sandesh')
ControlNodeSandeshGenSrcs = env.ExtractCpp(ControlNodeSandeshGenFiles)

# Generate the sandesh UVE source files
SandeshGenPyFiles  = env.SandeshGenPy('sandesh/control_node.sandesh')

setup_sources = [
           'setup.py',
           'MANIFEST.in',
          ]

setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(env.Install("", "#controller/src/control-node/sandesh/" + file))

sandesh_dirs = ['control_node']
for file in sandesh_dirs:
    Dir(env['TOP'] + '/control-node/gen_py/' + file)
sandesh_files = sandesh_dirs
sandesh_sources = ['gen_py/' + ssrc for ssrc in sandesh_files]
sandesh_sources_rules = []
for file in sandesh_sources:
    sandesh_sources_rules.append(env.Install("control_node", env['TOP'] + "/control-node/" + file))
env.Depends(sandesh_sources_rules, env['TOP'] + '/control-node/gen_py/')

sandesh_init_rules = [env.Install("control_node", env['TOP'] + "/control-node/gen_py/__init__.py")]
env.Requires(sandesh_init_rules, sandesh_sources_rules)

# Generate the sandesh cpuinfo from base
sandesh_base_gen_py_files = env.SandeshGenPy('#controller/src/base/sandesh/cpuinfo.sandesh')
sandesh_base_dirs = [ 'cpuinfo' ]
for file in sandesh_base_dirs:
    Dir(env['TOP'] + '/control-node/gen_py/' + file)
sandesh_base_files = sandesh_base_dirs
sandesh_base_sources = ['gen_py/' + ssrc for ssrc in sandesh_base_files]
sandesh_base_sources_rules = []
for file in sandesh_base_sources:
    sandesh_base_sources_rules.append(env.Install("control_node/control_node/", env['TOP'] + "/control-node/" + file))
env.Requires(sandesh_base_sources_rules, sandesh_base_gen_py_files)
env.Requires(sandesh_base_sources_rules, Dir(env['TOP'] + '/base/sandesh/gen_py/'))

# Generate the sandesh ifmap from third-party
sandesh_base_gen_py_files = env.SandeshGenPy('#controller/src/ifmap/ifmap_server_show.sandesh')
sandesh_base_dirs = [ 'ifmap_server_show' ]
for file in sandesh_base_dirs:
    Dir(env['TOP'] + '/control-node/gen_py/' + file)
sandesh_base_files = sandesh_base_dirs
sandesh_base_sources = ['gen_py/' + ssrc for ssrc in sandesh_base_files]
sandesh_base_sources_rules = []
for file in sandesh_base_sources:
    sandesh_base_sources_rules.append(env.Install("control_node/control_node/", env['TOP'] + "/control-node/" + file))
env.Requires(sandesh_base_sources_rules, sandesh_base_gen_py_files)
env.Requires(sandesh_base_sources_rules, Dir(env['TOP'] + '/src/ifmap/gen_py/'))



sandesh_common_dirs = [ 'vns' ]
for file in sandesh_common_dirs:
    Dir(env['TOP'] + '/sandesh/common/gen_py/' + file)
sandesh_common_files = sandesh_common_dirs 
sandesh_common_sources = ['gen_py/' + ssrc for ssrc in sandesh_common_files]
sandesh_common_sources_rules = []
for file in sandesh_common_sources:
    sandesh_common_sources_rules.append(env.Install("control_node", env['TOP'] + "/sandesh/common/" + file))
env.Depends(sandesh_common_sources_rules, Dir(env['TOP'] + '/sandesh/common/gen_py/'))

rel_path = Dir('control_node').path
def BuildInfoAction(target, source, env):
    env.GenerateBuildInfoPyCode(path=rel_path)
    
build_info_rules = [env.Command(target='buildinfo.py', source = None, action=BuildInfoAction)]

sdist_depends = []
sdist_depends.extend(setup_sources_rules)
sdist_depends.extend(sandesh_sources_rules)
sdist_depends.extend(sandesh_init_rules)
sdist_depends.extend(sandesh_common_sources_rules)
sdist_depends.extend(sandesh_base_sources_rules)
sdist_depends.extend(build_info_rules)
sdist_gen = env.Command('dist', 'setup.py', 'python setup.py sdist', chdir=1)

env.Depends(sdist_gen, sdist_depends)
env.Default(sdist_gen)
env.Alias("control-node:node_mgr", sdist_gen)

control_node_sandesh_files_ = []
for src in ControlNodeSandeshGenSrcs:
    objname = src.replace('.cpp', '.o')
    obj = env.Object(objname, src)
    control_node_sandesh_files_.append(obj)

env.Prepend(LIBS=['bgp',
                  'bgp_schema',
                  'xmpp_unicast',
                  'xmpp_multicast',
                  'xmpp_enet',
                  'control_node',
                  'peer_sandesh',
                  'origin_vn',
                  'routing_instance',
                  'rtarget',
                  'security_group',
                  'tunnel_encap',
                  'ifmap_vnc',
                  'ifmap_server',
                  'ifmap_common',
                  'route',
                  'net',
                  'ds',
                  'ifmapio',
                  'xmpp',
                  'xml',
                  'sandeshvns',
                  'sandesh',
                  'httpc',
                  'http',
                  'http_parser',
                  'curl',
                  'db',
                  'io',
                  'base',
                  'cpuinfo',
                  'pugixml',
                  'libxml2',
                  'boost_regex',
		  'boost_chrono',
                  'boost_program_options',
                  'boost_filesystem',
                  'crypto', 'ssl'])

if sys.platform != 'darwin':
    env.Append(LIBS=['rt'])

libs = MapBuildDir(['base',
                    'bgp',
                    'bgp/inet',
                    'bgp/inetmcast',
                    'bgp/enet',
                    'bgp/evpn',
                    'bgp/l3vpn',
                    'bgp/origin-vn',
                    'bgp/routing-instance',
                    'bgp/rtarget',
                    'bgp/security_group',
                    'bgp/tunnel_encap',
                    'control-node',
                    'db',
                    'ifmap',
                    'io', 
                    'net',
                    'route',
                    'xmpp',
                    'xml',
                    'discovery/client',
                    'http/client'])

env.Append(LIBPATH=libs)
env.Append(LIBPATH = env['TOP'] + '/schema')
env.Append(CPPPATH = env['TOP'])
env.Append(CPPPATH = env['TOP'] + '/control-node')
env.Append(CPPPATH = env['TOP'] + '/discovery/client')
env.Append(CPPPATH = env['TOP'] + '/io')

lib_l3vpn = File('../bgp/l3vpn/libbgp_l3vpn.a')
lib_inet = File('../bgp/inet/libbgp_inet.a')
lib_inetmcast = File('../bgp/inetmcast/libbgp_inetmcast.a')
lib_enet = File('../bgp/enet/libbgp_enet.a')
lib_evpn = File('../bgp/evpn/libbgp_evpn.a')
lib_rtarget = File('../bgp/rtarget/librtarget.a')
lib_ifmap_server = File('../ifmap/libifmap_server.a')
lib_sandesh = File('../sandesh/library/cpp/libsandesh.a')
lib_cpuinfo = File('../base/libcpuinfo.a')

if sys.platform != 'darwin':
    env.Prepend(LINKFLAGS =
                     ['-Wl,--whole-archive',
                      '-lbgp_l3vpn', '-lbgp_inet', '-lbgp_inetmcast',
                      '-lbgp_enet', '-lbgp_evpn', '-lrtarget',
                      '-lifmap_server', '-lcpuinfo',
                      '-Wl,--no-whole-archive'])
else:
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_l3vpn.path])
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_inet.path])
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_inetmcast.path])
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_enet.path])
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_evpn.path])
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_rtarget.path])
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_ifmap_server.path])
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_sandesh.path])
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_cpuinfo.path])

buildinfo_dep_libs = ['../bgp/libbgp.a', '../schema/libbgp_schema.a', '../schema/libxmpp_unicast.a', 
                      '../schema/libxmpp_multicast.a', '../schema/libxmpp_enet.a',
                      '../control-node/libcontrol_node.a', '../bgp/routing-instance/librouting_instance.a', 
                      '../bgp/origin-vn/liborigin_vn.a',
                      '../bgp/rtarget/librtarget.a', '../bgp/security_group/libsecurity_group.a', '../schema/libifmap_vnc.a', 
                      '../ifmap/libifmap_server.a', '../ifmap/libifmap_common.a', '../route/libroute.a', '../net/libnet.a', 
                      '../../lib/libifmapio.a', '../xmpp/libxmpp.a', '../xml/libxml.a', '../../lib/libsandeshvns.a', 
                      '../../lib/libsandesh.a', '../../lib/libhttp.a', '../../lib/libhttp_parser.a', '../../lib/libcurl.a', 
                      '../db/libdb.a', '../io/libio.a', '../base/libbase.a', '../base/libcpuinfo.a', '../../lib/libpugixml.a', 
                      '../../lib/libxml2.a', '../../lib/libboost_regex.a', '../../lib/libboost_program_options.a', 
                      '../../lib/libboost_filesystem.a', '../../lib/libboost_system.a', '../../lib/liblog4cplus.a']

if sys.platform != 'darwin':
    buildinfo_dep_libs += [ '../../lib/libtbb_debug.so.2' ]

env.GenerateBuildInfoCode(
	target = ['buildinfo.h', 'buildinfo.cc'],
	source = ControlNodeSandeshGenSrcs + buildinfo_dep_libs +
		['main.cc', 'options.cc', 'sandesh/control_node_sandesh.cc'],
	path = Dir('.').path)

ctrlnode = env.Program(target = 'control-node',
                       source = ['buildinfo.cc',
                                 'main.cc',
                                 'options.cc',
                                 'sandesh/control_node_sandesh.cc'] +
                                 control_node_sandesh_files_)

env.Default(ctrlnode)

env.Depends(ctrlnode, lib_l3vpn)
env.Depends(ctrlnode, lib_inet)
env.Depends(ctrlnode, lib_inetmcast)
env.Depends(ctrlnode, lib_enet)
env.Depends(ctrlnode, lib_evpn)
env.Depends(ctrlnode, lib_rtarget)
env.Depends(ctrlnode, lib_ifmap_server)

env.Alias('control-node', ctrlnode)

env.SConscript('test/SConscript', exports='BuildEnv', duplicate = 0)

env.Alias('install', env.Install(env['INSTALL_BIN'], ctrlnode))
env.Alias('install', env.Install(env['INSTALL_CONF'], 'control-node.conf'))
