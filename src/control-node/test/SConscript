#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

# -*- mode: python; -*-
Import('BuildEnv')
import sys

env = BuildEnv.Clone()
env.Prepend(CPPPATH = [env['TOP'],
                       env['TOP'] + '/db',
                       env['TOP'] + '/io',
                       env['TOP'] + '/tools/sandesh/library/common'
                      ])

libcontrol_test = env.Library('control_test',
                              ['control_node_test.cc',
                               'network_agent_mock.cc'])

env.Append(LIBPATH = env['TOP'] + '/base')
env.Append(LIBPATH = env['TOP'] + '/io')
env.Append(LIBPATH = env['TOP'] + '/net')

env.Prepend(LIBS = ['gunit', 'io', 'sandesh', 'http', 'net',
                    'process_info', 'io', 'base', 'http_parser', 'curl',
                    'ssl', 'crypto', 'boost_filesystem',
                    'boost_program_options', 'pugixml', 'sandeshvns'])

if sys.platform != 'darwin':
    env.Append(LIBS = ['rt'])

options_test = env.UnitTest('options_test', ['../buildinfo.o', '../options.o',
                                             'options_test.cc'])
env.Alias('src/control-node:options_test', options_test)

test_suite = [ options_test ]

test = env.TestSuite('control-node-test', test_suite)
env.Alias('controller/src/control-node:test', test)
Return('test_suite')
