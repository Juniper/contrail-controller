# -*- mode: python; -*-
#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('BuildEnv')
env = BuildEnv.Clone()

cd_cmd = 'cd ' + Dir('.').path + ' && '

autogen_infra_sources = [
                 '#/src/contrail-api-client/generateds/generateDS.py',
                 '#/src/contrail-api-client/generateds/TypeGenerator.py',
                 '#/src/contrail-api-client/generateds/ccmap.py',
                 '#/src/contrail-api-client/generateds/idl_parser.py',
                 '#/src/contrail-api-client/generateds/ifmap_global.py',
                 '#/src/contrail-api-client/generateds/ifmap_model.py',
                 '#/src/contrail-api-client/generateds/ifmap_frontend.py',
                ]

autogen_sources = [
                 '#src/contrail-api-client/schema/all_cfg.xsd',
                 '#src/contrail-api-client/schema/vnc_cfg.xsd',
                 '#src/contrail-api-client/schema/bgp_schema.xsd',
                 '#src/contrail-api-client/schema/ietf-l3vpn-schema.xsd',
                 '#src/contrail-api-client/schema/smi-base.xsd',
                ]

autogen_script = File('#src/contrail-api-client/generateds/generateDS.py').path
autogen_cmd = '%s -f -o %s -g ifmap-frontend' % (autogen_script, Dir('vnc_api/gen/resource').path)

generated_files = [ 
                  'vnc_api/gen/__init__.py',
                  'vnc_api/gen/resource_xsd.py',
                  'vnc_api/gen/resource_common.py',
                  'vnc_api/gen/resource_client.py',
                  'vnc_api/gen/vnc_api_client_gen.py',
                  'vnc_api/gen/contrail_openapi.rst',
                  'vnc_api/gen/contrail_openapi.json',
                ]
env.Append(ENV = {'HEAT_BUILDTOP' : Dir(env['TOP']).abspath})

generated_rule = env.Command(generated_files,
                             '#src/contrail-api-client/schema/all_cfg.xsd',
                             autogen_cmd + ' $SOURCE')

setup_sources = [
           'setup.py',
           '.testr.conf',
           '.coveragerc',
           'run_tests.sh',
           'tools',
           'tests',
           'requirements.txt',
           'test-requirements.txt',
          ]

setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(env.Install("", "#/controller/src/api-lib/" + file))

remote_setup_sources = [
           '#/controller/src/base/version.info'
          ]

remote_setup_sources_rules = []
for file in remote_setup_sources:
    remote_setup_sources_rules.append(env.Install("vnc_api", file))

local_sources = [
           '__init__.py',
           'vnc_api.py',
           'vnc_api_lib.ini',
           'utils.py',
           'ssl_adapter.py',
           'exceptions.py',
          ]

local_sources_rules = []
for file in local_sources:
    local_sources_rules.append(env.Install("vnc_api", "#/controller/src/api-lib/" + file))

generateds_rule = env.Install("vnc_api/gen", "#/controller/src/config/common/generatedssuper.py")
cfixture_rule = env.Install("vnc_api/gen", "#/controller/src/config/common/cfixture.py")

sdist_depends = [generated_rule, generateds_rule, cfixture_rule]
sdist_depends.extend(setup_sources_rules)
sdist_depends.extend(remote_setup_sources_rules)
sdist_depends.extend(local_sources_rules)
sdist_gen = env.Command('dist/vnc_api-{}.tar.gz'\
                        .format(open(File('#/controller/src/base/version.info').abspath, 'r')\
                        .read().strip('\n').strip('\t')),
                        'setup.py', cd_cmd + 'python setup.py sdist')

env.Default(sdist_gen)

# generated files depend on autogeneration infra parts
env.Depends(generated_rule, [autogen_infra_sources, autogen_sources, generateds_rule])
env.Depends(local_sources_rules, autogen_infra_sources)

# install everything before building distribution
env.Depends(sdist_gen, sdist_depends)

if 'install' in BUILD_TARGETS:
   install_cmd = env.Command(None, 'setup.py',
                             cd_cmd + 'python setup.py install %s' %
                             env['PYTHON_INSTALL_OPT'])
   env.Depends(install_cmd, sdist_depends)
   env.Alias('install', install_cmd)

env.Alias('install', env.Install(env['INSTALL_CONF'], 'vnc_api_lib.ini'))

env.SetupPyTestSuiteWithDeps(sdist_gen, '/config/common/dist/cfgm_common-0.1dev.tar.gz')

# Local Variables:
# mode: python
# End:
