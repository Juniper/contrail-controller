# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

Import('BuildEnv')
env = BuildEnv.Clone()

def SrcBuildDir(x):
    return '#/' + Dir('.').path + '/../../' + x;

env.Prepend(LIBS=['io', 'base', 'sandesh'])
env.Append(LIBPATH=['.', SrcBuildDir('base'), SrcBuildDir('io')])
env.Append(CPPPATH = env['TOP'])

# libsandeshflow
SandeshGenFiles = env.SandeshGenCpp('flow.sandesh')
SandeshGenSrcs = env.ExtractCpp(SandeshGenFiles)
SandeshGenHdrs = env.ExtractHeader(SandeshGenFiles)
libsandeshflow = env.Library(target = 'sandeshflow', source = SandeshGenSrcs)
env.Install(env['TOP_LIB'], libsandeshflow)
env.Install(env['TOP_INCLUDE'] + '/sandesh/common', SandeshGenHdrs)

#libsandeshvns
SandeshGenFiles = env.SandeshGenCpp('vns.sandesh')
SandeshGenSrcs = env.ExtractCpp(SandeshGenFiles)
SandeshGenHdrs = env.ExtractHeader(SandeshGenFiles)
libsandeshvns = env.Library(target = 'sandeshvns', source = SandeshGenSrcs)
env.Install(env['TOP_LIB'], libsandeshvns)
env.Install(env['TOP_INCLUDE'] + '/sandesh/common', SandeshGenHdrs)

# Generate the Python source files
SandeshGenPyFiles = env.SandeshGenPy('flow.sandesh')

gen_vns_files = env.SandeshGenPy('vns.sandesh')
setup_files = env.Install('sandesh_common/vns', gen_vns_files)
gen_init = env.Command('sandesh_common/__init__.py', None,
                       'mkdir -p sandesh_common; touch $TARGET')
env.Depends(setup_files, gen_vns_files)

env.Install('.', '#controller/src/sandesh/common/setup.py')
sdist_gen = env.Command('dist/sandesh-common-0.1dev.tar.gz',
                        ['setup.py'] + setup_files + gen_init,
                        '(cd %s; python setup.py sdist)' % Dir('.').path)

env.Default(sdist_gen)

