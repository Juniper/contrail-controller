#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

sources = [
    'setup.py',
    'requirements.txt',
    'run_tests.sh',
    'test-requirements.txt',
    '.testr.conf',
    '.coveragerc',
    'MANIFEST.in',
    'kube_manager/__init__.py',
    'kube_manager/kube_manager.py',
    'kube_manager/common/__init__.py',
    'kube_manager/common/logger.py',
    'kube_manager/kube/__init__.py',
    'kube_manager/kube/kube_monitor.py',
    'kube_manager/kube/namespace_monitor.py',
    'kube_manager/kube/network_policy_monitor.py',
    'kube_manager/kube/pod_monitor.py',
    'kube_manager/kube/service_monitor.py',
    'kube_manager/vnc/__init__.py',
    'kube_manager/vnc/config_db.py',
    'kube_manager/vnc/db.py',
    'kube_manager/vnc/label_cache.py',
    'kube_manager/vnc/reaction_map.py',
    'kube_manager/vnc/vnc_kubernetes.py',
    'kube_manager/vnc/vnc_namespace.py',
    'kube_manager/vnc/vnc_network_policy.py',
    'kube_manager/vnc/vnc_pod.py',
    'kube_manager/vnc/vnc_service.py',
    'kube_manager/tests/__init__.py',
]

cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_gen = env.Command('dist', sources, cd_cmd + 'python setup.py sdist')
env.Alias('controller/src/container/kube_manager:sdist', sdist_gen)

test_sources = sources
packages = [
    '%s/api-lib/dist/vnc_api-0.1dev.tar.gz' % env['TOP'],
    '%s/config/common/dist/cfgm_common-0.1dev.tar.gz' % env['TOP'],
    '%s/sandesh/common/dist/sandesh-common-0.1dev.tar.gz' % env['TOP'],
    '%s/tools/sandesh/library/python/dist/sandesh-0.1dev.tar.gz' % env['TOP'],
]

def BuildPyTestSetup(env, target, source):
    file = open(target[0].abspath, 'w')
    file.write("[easy_install]\nfind_links =")
    for pkg in source:
        dependency = env.File(pkg)
        file.write(" %s" % os.path.dirname(dependency.abspath))
    file.write("\n")
    file.close()
    return

# Documentation
doc_files = []

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None, sources,
                              cd_cmd + 'python setup.py install %s' %
                              env['PYTHON_INSTALL_OPT'])
    env.Alias('install', install_cmd)

env.Alias('install', env.Install(env['INSTALL_CONF'], 'contrail-kubernetes.conf'))
env.Alias('install', env.InstallAs( env['INSTALL_INITD'] +
    '/contrail-kube-manager', 'contrail-kube-manager.initd.supervisord'))
env.Alias('install', env.Install(env['INSTALL_CONF']+
    '/supervisord_config_files', 'contrail-kube-manager.ini'))
env.Alias('install', env.Install(
    env['INSTALL_MESSAGE_DOC'] + '/contrail-kube-manager/', doc_files))

buildspace_link = os.environ.get('CONTRAIL_REPO')
if buildspace_link:
    # in CI environment shebang limit exceeds for python
    # in easy_install/pip, reach to it via symlink
    top_path = buildspace_link + '/' + Dir('.').path
    top_dir = Dir(top_path)
else:
    top_dir = Dir('.')

# unit test
cov_cmd = env.Command('coveragetest.log', sdist_gen,
          'bash -c "set -o pipefail && cd ' + Dir(top_dir).path + ' && python setup.py run_tests --coverage 2>&1 | tee coveragetest.log"')

test_cmd = env.Command('test.log', sdist_gen,
          'bash -c "set -o pipefail && cd ' + Dir(top_dir).path + ' && python setup.py run_tests 2>&1 | tee test.log"')

test_depends = ['/config/common/dist/cfgm_common-0.1dev.tar.gz',
                '/api-lib/dist/vnc_api-0.1dev.tar.gz',
                '/tools/sandesh/library/python/dist/sandesh-0.1dev.tar.gz',
                '/sandesh/common/dist/sandesh-common-0.1dev.tar.gz',
                '/config/api-server/dist/vnc_cfg_api_server-0.1dev.tar.gz',]
env.Depends(test_cmd, [env['TOP']+x for x in test_depends])
env.Depends(cov_cmd, [env['TOP']+x for x in test_depends])

env.Alias('test', test_cmd)
env.Alias('coverage', cov_cmd)

env.Alias('controller/src/container/kube_manager:test', test_cmd)
env.Alias('controller/src/container/kube_manager:coverage', cov_cmd)
# This add'l alias is to allow retry of UT in CI
env.Alias('controller/src/container/kube-manager/kube_manager:test', test_cmd)

# Local Variables:
# mode: python
# End:
