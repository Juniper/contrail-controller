# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

setup_sources = [
    'setup.py',
    'requirements.txt',
    'run_tests.sh',
    'test-requirements.txt',
    '.testr.conf',
    '.coveragerc',
    'MANIFEST.in',
]

setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(
        env.Install(Dir('.'), "#controller/src/container/kube-manager/" + file))

local_sources = [
    'kube_manager/__init__.py',
    'kube_manager/kube_manager.py',
    'kube_manager/common/__init__.py',
    'kube_manager/common/logger.py',
    'kube_manager/common/args.py',
    'kube_manager/common/kube_config_db.py',
    'kube_manager/common/utils.py',
    'kube_manager/kube/__init__.py',
    'kube_manager/kube/kube_monitor.py',
    'kube_manager/kube/namespace_monitor.py',
    'kube_manager/kube/network_policy_monitor.py',
    'kube_manager/kube/pod_monitor.py',
    'kube_manager/kube/service_monitor.py',
    'kube_manager/kube/endpoint_monitor.py',
    'kube_manager/kube/ingress_monitor.py',
    'kube_manager/vnc/__init__.py',
    'kube_manager/vnc/config_db.py',
    'kube_manager/vnc/db.py',
    'kube_manager/vnc/label_cache.py',
    'kube_manager/vnc/reaction_map.py',
    'kube_manager/vnc/vnc_kubernetes.py',
    'kube_manager/vnc/vnc_kubernetes_config.py',
    'kube_manager/vnc/vnc_common.py',
    'kube_manager/vnc/vnc_namespace.py',
    'kube_manager/vnc/vnc_network_policy.py',
    'kube_manager/vnc/vnc_pod.py',
    'kube_manager/vnc/vnc_service.py',
    'kube_manager/vnc/vnc_endpoints.py',
    'kube_manager/vnc/vnc_ingress.py',
    'kube_manager/vnc/loadbalancer.py',
    'kube_manager/vnc/link_local_manager.py',
    'kube_manager/vnc/flow_aging_manager.py',
    'kube_manager/tests/__init__.py',
    'kube_manager/tests/vnc/__init__.py',
    'kube_manager/tests/vnc/db_mock.py',
    'kube_manager/tests/vnc/test_case.py',
    'kube_manager/tests/vnc/test_config_db.py',
    'kube_manager/tests/vnc/test_vnc_endpoints.py',
    'kube_manager/tests/vnc/test_vnc_kubernetes.py',
    'kube_manager/tests/vnc/test_vnc_namespace.py',
    'kube_manager/tests/vnc/test_vnc_pod.py',
    'kube_manager/tests/vnc/test_vnc_service.py',
    'kube_manager/tests/vnc/vnc_api_mock.py',
    'kube_manager/tests/kube/__init__.py',
]

# Sandesh source files.
sandesh_sources = []
sandesh_sources += env.SandeshGenPy('kube_manager.sandesh',
                                    'kube_manager/sandesh/', False)
sandesh_sources += env.SandeshGenPy('kube_introspect.sandesh',
                                    'kube_manager/sandesh/', False)

local_sources_rules = []
for file in local_sources:
    target = "/".join(file.split('/')[:-1])
    local_sources_rules.append(
        env.Install(target, "#controller/src/container/kube-manager/" + file))

# Documentation
doc_files = []
doc_files += env.SandeshGenDoc('kube_manager.sandesh')
doc_files += env.SandeshGenDoc('kube_introspect.sandesh')

cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_depends = []
sdist_depends.extend(setup_sources_rules)
sdist_depends.extend(local_sources_rules)
sdist_depends.extend(sandesh_sources)
sdist_gen = env.Command('dist/kube_manager-0.1dev.tar.gz', 'setup.py',
                        cd_cmd + 'python setup.py sdist')

env.Depends(sdist_gen, sdist_depends)
env.Default(sdist_gen)

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None, 'setup.py',
                              cd_cmd + 'python setup.py install %s' %
                              env['PYTHON_INSTALL_OPT'])
    env.Depends(install_cmd, sdist_depends)
    env.Alias('install', install_cmd)

buildspace_link = os.environ.get('CONTRAIL_REPO')
if buildspace_link:
    # in CI environment shebang limit exceeds for python
    # in easy_install/pip, reach to it via symlink
    top_path = buildspace_link + '/' + Dir('.').path
    top_dir = Dir(top_path)
else:
    top_dir = Dir('.')

# unit test
cov_cmd = env.Command('coveragetest.log', sdist_gen,
          'bash -c "set -o pipefail && cd ' + Dir(top_dir).path + ' && python setup.py run_tests --coverage 2>&1 | tee coveragetest.log"')

test_cmd = env.Command('test.log', sdist_gen,
          'bash -c "set -o pipefail && cd ' + Dir(top_dir).path + ' && python setup.py run_tests 2>&1 | tee test.log"')

test_depends = ['/config/common/dist/cfgm_common-0.1dev.tar.gz',
                '/api-lib/dist/vnc_api-0.1dev.tar.gz',
                '/tools/sandesh/library/python/dist/sandesh-0.1dev.tar.gz',
                '/sandesh/common/dist/sandesh-common-0.1dev.tar.gz',
                '/config/api-server/dist/vnc_cfg_api_server-0.1dev.tar.gz',]
env.Depends(test_cmd, [env['TOP']+x for x in test_depends])
env.Depends(cov_cmd, [env['TOP']+x for x in test_depends])

env.Alias('test', test_cmd)
env.Alias('coverage', cov_cmd)

env.Alias('install', env.Install(
    env['INSTALL_MESSAGE_DOC'] + '/contrail-kube-manager/', doc_files))


env.Alias('install', env.Install(env['INSTALL_CONF'],
    'supervisord_kubernetes.conf'))
env.Alias('install', env.Install(env['INSTALL_CONF'] +
    '/supervisord_kubernetes_files', 'contrail-kube-manager.ini'))
env.Alias('install', env.InstallAs(env['INSTALL_INITD'] +
    '/contrail-kube-manager', 'contrail-kube-manager.initd.supervisord'))

env.Alias('install', env.InstallAs(env['INSTALL_SYSTEMD'] +
          '/contrail-kube-manager.service', 'systemd/contrail-kube-manager.service'))

env.Alias('controller/src/container/kube-manager:test', test_cmd)
env.Alias('controller/src/container/kube-manager:coverage', cov_cmd)

# Local Variables:
# mode: python
# End:
