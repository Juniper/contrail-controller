# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

setup_sources = [
    'setup.py',
    'requirements.txt',
    'run_tests.sh',
    'test-requirements.txt',
    '.testr.conf',
    '.coveragerc',
    'MANIFEST.in',
    'README.txt',
]

setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(
        env.Install(Dir('.'), "#controller/src/container/mesos-manager/" + file))

local_sources = [
    'mesos_manager/__init__.py',
    'mesos_manager/mesos_cni.py',
    'mesos_manager/mesos_consts.py',
    'mesos_manager/mesos_manager.py',
    'mesos_manager/mesos_server.py',
    'mesos_manager/common/__init__.py',
    'mesos_manager/common/logger.py',
    'mesos_manager/common/args.py',
    'mesos_manager/vnc/__init__.py',
    'mesos_manager/vnc/vnc_mesos.py',
    'mesos_manager/vnc/vnc_services.py',
    'mesos_manager/vnc/db.py',
    'mesos_manager/vnc/config_db.py',
    'mesos_manager/vnc/reaction_map.py',
    'mesos_manager/tests/__init__.py',
    'mesos_manager/tests/test_mesos_manager.py',
]

# Sandesh source files.
sandesh_sources = []
sandesh_sources += env.SandeshGenPy('mesos_manager.sandesh',
                                    'mesos_manager/sandesh/', False)

local_sources_rules = []
for file in local_sources:
    target = "/".join(file.split('/')[:-1])
    local_sources_rules.append(
        env.Install(target, "#controller/src/container/mesos-manager/" + file))

# Documentation
doc_files = []
doc_files += env.SandeshGenDoc('mesos_manager.sandesh')
doc_files += env['CFGM_DOC_FILES']

cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_depends = []
sdist_depends.extend(setup_sources_rules)
sdist_depends.extend(local_sources_rules)
sdist_depends.extend(sandesh_sources)
sdist_gen = env.Command('dist/mesos_manager-0.1dev.tar.gz', 'setup.py',
                        cd_cmd + 'python setup.py sdist')

env.Depends(sdist_gen, sdist_depends)
env.Default(sdist_gen)

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None, 'setup.py',
                              cd_cmd + 'python setup.py install %s' %
                              env['PYTHON_INSTALL_OPT'])
    env.Depends(install_cmd, sdist_depends)
    env.Alias('install', install_cmd)

buildspace_link = os.environ.get('CONTRAIL_REPO')
if buildspace_link:
    # in CI environment shebang limit exceeds for python
    # in easy_install/pip, reach to it via symlink
    top_path = buildspace_link + '/' + Dir('.').path
    top_dir = Dir(top_path)
else:
    top_dir = Dir('.')

# unit test
cov_cmd = env.Command('coveragetest.log', sdist_gen,
          'bash -c "set -o pipefail && cd ' + Dir(top_dir).path + ' && python setup.py run_tests --coverage 2>&1 | tee coveragetest.log"')

test_cmd = env.Command('test.log', sdist_gen,
          'bash -c "set -o pipefail && cd ' + Dir(top_dir).path + ' && python setup.py run_tests 2>&1 | tee test.log"')

test_depends = ['/config/common/dist/cfgm_common-0.1dev.tar.gz',
                '/api-lib/dist/vnc_api-0.1dev.tar.gz',
                '/tools/sandesh/library/python/dist/sandesh-0.1dev.tar.gz',
                '/sandesh/common/dist/sandesh-common-0.1dev.tar.gz',
                '/config/api-server/dist/vnc_cfg_api_server-0.1dev.tar.gz',]
env.Depends(test_cmd, [env['TOP']+x for x in test_depends])
env.Depends(cov_cmd, [env['TOP']+x for x in test_depends])

env.Alias('test', test_cmd)
env.Alias('coverage', cov_cmd)

env.Alias('install', env.Install(
    env['INSTALL_MESSAGE_DOC'] + '/contrail-mesos-manager/', doc_files))

env.Alias('controller/src/container/mesos_manager:test', test_cmd)
env.Alias('controller/src/container/mesos_manager:coverage', cov_cmd)
# This add'l alias is to allow retry of UT in CI
env.Alias('controller/src/container/mesos-manager/mesos_manager:test', test_cmd)

# Local Variables:
# mode: python
# End:
