# -*- mode: python; -*-

#
# Copyright (c) 2013 Contrail Systems. All rights reserved.
#

import glob

Import('CfgmEnv')
env = CfgmEnv.Clone()

source_files = ['setup.py',
                'vnc_openstack/__init__.py',
                'vnc_openstack/neutron_plugin_db.py',
                'vnc_openstack/neutron_plugin_interface.py']
source_rules = []
for file in source_files:
    if len(file.split('/')) > 1:
        idir = '/'.join(file.split('/')[:-1])
    else:
        idir = ''
    source_rules.append(
        env.Install(idir, "#/controller/src/config/vnc_openstack/" + file))

cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_depends = []
sdist_depends.extend(source_rules)
sdist_gen = env.Command('dist', 'setup.py', cd_cmd + 'python setup.py sdist')

env.Depends(sdist_gen, sdist_depends)
env.Default(sdist_gen)

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None, 'setup.py',
                              cd_cmd + 'python setup.py install %s' %
                              env['PYTHON_INSTALL_OPT'])
    env.Depends(install_cmd, sdist_depends)
    env.Alias('install', install_cmd)
