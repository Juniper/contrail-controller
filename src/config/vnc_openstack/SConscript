#
# Copyright (c) 2013 Contrail Systems. All rights reserved.
#

import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

source_files = ['.coveragerc',
                '__init__.py',
                'requirements.txt',
                'setup.py',
                '.stestr.conf',
                'test-requirements.txt',
                'tox.ini',
                Glob('vnc_openstack/*.py'),
                Glob('vnc_openstack/tests/*.py')]

cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_gen = env.Command('dist/vnc_openstack-0.1dev.tar.gz',
                        source_files,
                        cd_cmd + 'python setup.py sdist')
env.Depends(sdist_gen, source_files)
env.Default(sdist_gen)

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None,
                              'setup.py',
                              cd_cmd + 'python setup.py install %s' % env['PYTHON_INSTALL_OPT'])
    env.Alias('install', install_cmd)

test_target = env.SetupPyTestSuite(sdist_gen, '/config/api-server/dist/contrail-api-server-0.1dev.tar.gz', use_tox=True)

# This add'l alias is to allow retry of UT in CI
env.Alias('controller/src/config/vnc_openstack/vnc_openstack:test', test_target)

# Local Variables:
# mode: python
# End:
