# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

setup_sources = [
    '.coveragerc',
    '.stestr.conf',
    'setup.py',
    'requirements.txt',
    'test-requirements.txt',
    'tox.ini',
    'MANIFEST.in',
    'cfgm_common',
]

setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(
        env.Install(Dir('.'), "#controller/src/config/common/" + file))

pkg = [
    env.SandeshGenPy('#controller/src/config/uve/service_instance.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/vnc_api.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/virtual_machine.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/virtual_network.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/physical_router.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/acl.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/cfgm_cpuinfo.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/greenlets.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/msg_traces.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/config_req.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/physical_router_config.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/service_status.sandesh', 'cfgm_common/uve/', False),
    env.SandeshGenPy('#controller/src/config/uve/feature_flags.sandesh', 'cfgm_common/uve/', False),
]

# A set of scripts used by other python tests - they are expected in #build
common_test_tools = [
    'tests/tools/colorizer.py',
    'tests/tools/install_venv.py',
    'tests/tools/install_venv_common.py',
    'tests/tools/run_tests.sh',
    'tests/tools/with_venv.sh',
]

common_test_tools_rules = []
for file in common_test_tools:
    common_test_tools_rules.append(
        env.Install(Dir('.').abspath + '/tests/tools/', "#controller/src/config/common/" + file))

# Generate the sandesh cpuinfo from base
nodeinfo_pkg = env.SandeshGenPy('#src/contrail-common/base/sandesh/nodeinfo.sandesh', 'cfgm_common/uve/', False)
cpuinfo_pkg = env.SandeshGenPy('#src/contrail-common/base/sandesh/cpuinfo.sandesh', 'cfgm_common/uve/nodeinfo/', False)
process_info_pkg = env.SandeshGenPy('#src/contrail-common/base/sandesh/process_info.sandesh', 'cfgm_common/uve/nodeinfo/', False)
env.Depends(nodeinfo_pkg, pkg)
env.Depends(cpuinfo_pkg, pkg)
env.Depends(process_info_pkg, pkg)

rel_path = Dir('cfgm_common').path
def BuildInfoAction(target, source, env):
    env.GenerateBuildInfoPyCode(path=rel_path)
build_info_rules = [env.Command(target='cfgm_common/buildinfo.py', source = None, action=BuildInfoAction)]

sdist_depends = []
sdist_depends.extend(setup_sources_rules)
sdist_depends.extend(common_test_tools_rules)
sdist_depends.extend(pkg)
sdist_depends.extend(nodeinfo_pkg)
sdist_depends.extend(cpuinfo_pkg)
sdist_depends.extend(process_info_pkg)
sdist_depends.extend(build_info_rules)

cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_gen = env.Command('dist/contrail-config-common-0.1dev.tar.gz',
                        'setup.py', cd_cmd + 'python setup.py sdist')

# Add the dependency between contrail-config-common tarball and its dependencies.
# That lets others depend only on contrail-config-common without having to also
# depend on vnc_api.
env.Depends(sdist_gen, sdist_depends)
env.Alias('controller/config/common:sdist', sdist_gen)
env.Default(sdist_gen)

def SymLink(target, source, env):
    os.symlink(os.path.abspath(str(source[0])), os.path.abspath(str(target[0])))

# We can't use SetupPyTestSuite because it adds ourselves to dependencies
env.SetupPyTestSuite(sdist_gen, use_tox=True)

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None, 'setup.py',
                              cd_cmd + 'python setup.py install %s' %
                              env['PYTHON_INSTALL_OPT'])
    env.Depends(install_cmd, sdist_gen)
    env.Alias('install', install_cmd)
    # Install in python3 path
    setup_link_cmd = env.Command("setup3.py", "setup.py", SymLink)
    env.Depends(setup_link_cmd, sdist_depends)
    py3_install_cmd = env.Command(None, 'setup3.py',
                                  cd_cmd + 'python3 setup3.py install %s' %
                                  env['PYTHON_INSTALL_OPT'])
    env.Depends(py3_install_cmd, sdist_depends)
    env.Alias('install', py3_install_cmd)
