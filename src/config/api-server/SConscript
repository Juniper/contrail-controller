# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

autogen_infra_sources = [
                 '#/src/contrail-api-client/generateds/generateDS.py',
                 '#/src/contrail-api-client/generateds/TypeGenerator.py',
                 '#/src/contrail-api-client/generateds/ccmap.py',
                 '#/src/contrail-api-client/generateds/idl_parser.py',
                 '#/src/contrail-api-client/generateds/ifmap_global.py',
                 '#/src/contrail-api-client/generateds/ifmap_model.py',
                 '#/src/contrail-api-client/generateds/ifmap_frontend.py',
                ]

autogen_sources = [
                 '#src/contrail-api-client/schema/all_cfg.xsd',
                 '#src/contrail-api-client/schema/vnc_cfg.xsd',
                 '#src/contrail-api-client/schema/bgp_schema.xsd',
                 '#src/contrail-api-client/schema/ietf-l3vpn-schema.xsd',
                 '#src/contrail-api-client/schema/smi-base.xsd',
                ]

generated_dependencies = [
                  env['TOP'] + '/api-lib/vnc_api/gen/__init__.py',
                  env['TOP'] + '/api-lib/vnc_api/gen/resource_xsd.py',
                  env['TOP'] + '/api-lib/vnc_api/gen/resource_common.py',
                  env['TOP'] + '/api-lib/vnc_api/gen/resource_client.py',
                  env['TOP'] + '/api-lib/vnc_api/gen/vnc_api_client_gen.py',
                ]

setup_sources = [
    '.coveragerc',
    '.pylintrc',
    '.testr.conf',
    'MANIFEST.in',
    'README.md',
    'requirements.txt',
    'setup.py',
    'setup.cfg',
    'test-requirements.txt',
    'tox.ini',
    'vnc_cfg_api_server',
    'doc',
]

setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(
        env.Install(Dir('.'), "#controller/src/config/api-server/" + file))

autogen_script = File('#src/contrail-api-client/generateds/generateDS.py').path
autogen_cmd = '%s -f -o %s -g ifmap-frontend' % (autogen_script, Dir('vnc_cfg_api_server/gen/resource').path)

generated_files = ['vnc_cfg_api_server/gen/__init__.py']
env.Append(ENV = {'HEAT_BUILDTOP' : Dir(env['TOP']).abspath})

generated_rule = env.Command(generated_files,
                             '#/src/contrail-api-client/schema/all_cfg.xsd',
                             autogen_cmd + ' $SOURCE')

generateds_rule = env.Install("vnc_cfg_api_server/gen", "../common/generatedssuper.py")
cfixture_rule = env.Install("vnc_cfg_api_server/gen", "../common/cfixture.py")

doc_sources_rules = SConscript(dirs=['doc'], exports = 'CfgmEnv')

sandesh_trace_pkg = env.SandeshGenPy('traces.sandesh', 'vnc_cfg_api_server/sandesh/', False)

sdist_depends = [generated_rule, generateds_rule, cfixture_rule]
sdist_depends.extend(setup_sources_rules)
sdist_depends.extend(doc_sources_rules)
sdist_depends.extend(sandesh_trace_pkg)

cd_cmd = 'cd ' + Dir('.').path + ' && '
# TODO: deprecate
sdist_gen = env.Command('dist/contrail-api-server-0.1dev.tar.gz', 'setup.py',
                        cd_cmd + 'python setup.py sdist')

env.Default(sdist_gen)

# generated files depend on autogeneration infra parts

env.Depends(generated_rule, [autogen_infra_sources, autogen_sources, generated_dependencies])

# install everything before building distribution
env.Depends(sdist_gen, sdist_depends)

# Documentation
doc_files = []
doc_files += env.SandeshGenDoc('traces.sandesh')
doc_files += env['CFGM_DOC_FILES']

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None, 'setup.py',
                              cd_cmd + 'python setup.py install %s' %
                              env['PYTHON_INSTALL_OPT'])
    env.Depends(install_cmd, sdist_gen)
    env.Alias('install', install_cmd)

env.Alias('install', env.Install(env['INSTALL_CONF'], 'contrail-api.conf'))
env.Alias('install', env.InstallAs(env['INSTALL_INITD'] +
    '/contrail-api', 'contrail-api.initd.supervisord'))
env.Alias('install', env.InstallAs(env['INSTALL_SYSTEMD'] +
    '/contrail-api.service', 'systemd/contrail-api.service'))
env.Alias('install', env.InstallAs(env['INSTALL_SYSTEMD'] +
    '/contrail-config-nodemgr.service', 'systemd/contrail-config-nodemgr.service'))
env.Alias('install', env.Install(env['INSTALL_CONF'],
    'supervisord_config.conf'))
env.Alias('install', env.Install(env['INSTALL_CONF']+
    '/supervisord_config_files', 'contrail-api.ini'))
env.Alias('install', env.Install(env['INSTALL_CONF']+
    '/supervisord_config_files', 'contrail-config.rules'))
env.Alias('install', env.Install(
    env['INSTALL_MESSAGE_DOC'] + '/contrail-api/', doc_files))

env.SetupPyTestSuite(sdist_gen, use_tox=True)

# Local Variables:
# mode: python
# End:
