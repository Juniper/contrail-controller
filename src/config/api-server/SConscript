# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#
import os

Import('CfgmEnv')
env = CfgmEnv.Clone()

autogen_infra_sources = [
                 '#/tools/generateds/generateDS.py',
                 '#/tools/generateds/TypeGenerator.py',
                 '#/tools/generateds/ccmap.py',
                 '#/tools/generateds/idl_parser.py',
                 '#/tools/generateds/ifmap_global.py',
                 '#/tools/generateds/ifmap_model.py',
                 '#/tools/generateds/ifmap_frontend.py',
                ]

autogen_sources = [
                 '#controller/src/schema/all_cfg.xsd',
                 '#controller/src/schema/vnc_cfg.xsd',
                 '#controller/src/schema/bgp_schema.xsd',
                 '#controller/src/schema/ietf-l3vpn-schema.xsd',
                 '#controller/src/schema/smi-base.xsd',
                ]

autogen_script = File('#tools/generateds/generateDS.py').path
autogen_cmd = '%s -f -o %s -g ifmap-frontend' % (autogen_script, Dir('vnc_cfg_api_server/gen/resource').path)

generated_files = [ 
                  'vnc_cfg_api_server/gen/__init__.py',
                  'vnc_cfg_api_server/gen/resource_xsd.py',
                  'vnc_cfg_api_server/gen/resource_common.py',
                  'vnc_cfg_api_server/gen/resource_server.py',
                  'vnc_cfg_api_server/gen/resource_test.py',
                  'vnc_cfg_api_server/gen/vnc_api_server_gen.py',
                  'vnc_cfg_api_server/gen/vnc_ifmap_client_gen.py',
                  'vnc_cfg_api_server/gen/vnc_cassandra_client_gen.py',
                ]
generated_rule = env.Command(generated_files,
                             '#/controller/src/schema/all_cfg.xsd',
                             autogen_cmd + ' $SOURCE')

setup_sources = [
           'setup.py',
           'MANIFEST.in',
           'run_tests.sh',
           'requirements.txt',
           'test-requirements.txt',
           '.testr.conf',
           'tests',
          ]

setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(
        env.Install(Dir('.'), "#controller/src/config/api-server/" + file))

local_sources = [
           '__init__.py',
           'vnc_cfg_api_server.py',
           'vnc_cfg_ifmap.py',
           'vnc_cfg_types.py',
           'vnc_auth.py',
           'vnc_auth_keystone.py',
           'vnc_perms.py',
           'vnc_addr_mgmt.py',
           'vnc_quota.py',
           'provision_defaults.py'
          ]

local_sources_rules = []
for file in local_sources:
    local_sources_rules.append(
        env.Install("vnc_cfg_api_server", "#controller/src/config/api-server/" + file))

generateds_rule = env.Install("vnc_cfg_api_server/gen", "../common/generatedssuper.py")
cfixture_rule = env.Install("vnc_cfg_api_server/gen", "../common/cfixture.py")

doc_sources_rules = SConscript(dirs=['doc'], exports = 'CfgmEnv')

sandesh_trace_pkg = env.SandeshGenPy('traces.sandesh', 'vnc_cfg_api_server/sandesh/', False)

sdist_depends = [generated_rule, generateds_rule, cfixture_rule]
sdist_depends.extend(setup_sources_rules)
sdist_depends.extend(local_sources_rules)
sdist_depends.extend(doc_sources_rules)
sdist_depends.extend(sandesh_trace_pkg)

# TODO: deprecate
sdist_gen = env.Command('dist/vnc_cfg_api_server-0.1dev.tar.gz', 'setup.py', 'python setup.py sdist', chdir=Dir('.'))

env.Default(sdist_gen)

# generated files depend on autogeneration infra parts

env.Depends(generated_rule, [autogen_infra_sources, autogen_sources])
env.Depends(local_sources_rules, autogen_infra_sources)

# install everything before building distribution
env.Depends(sdist_gen, sdist_depends)

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None, 'setup.py',
                              'python setup.py install %s' %
                              env['PYTHON_INSTALL_OPT'],
                              chdir = 1)
    env.Depends(install_cmd, sdist_gen)
    env.Alias('install', install_cmd)

env.Alias('install', env.Install(env['INSTALL_CONF'], 'contrail-api.conf'))
env.Alias('install', env.InstallAs(env['INSTALL_INITD'] + 
    '/contrail-api', 'contrail-api.initd.supervisord'))
env.Alias('install', env.Install(env['INSTALL_CONF'], 
    'supervisord_config.conf'))
env.Alias('install', env.Install(env['INSTALL_CONF']+ 
    '/supervisord_config_files', 'contrail-api.ini'))
env.Alias('install', env.Install(env['INSTALL_CONF']+ 
    '/supervisord_config_files', 'contrail-config.rules'))

buildspace_link = os.environ.get('CONTRAIL_REPO')
if buildspace_link:
    # in CI environment shebang limit exceeds for python
    # in easy_install/pip, reach to it via symlink
    top_path = buildspace_link + '/' + Dir('.').path
    top_dir = Dir(top_path)
else:
    top_dir = Dir('.')

test_cmd = env.Command('test.log', sdist_gen,
          'python setup.py run_tests 2>&1 | tee test.log',
          chdir=top_dir)

test_depends = ['/config/common/dist/cfgm_common-0.1dev.tar.gz',
                '/api-lib/dist/vnc_api-0.1dev.tar.gz',
                '/discovery/client/dist/discoveryclient-0.1dev.tar.gz',
                '/tools/sandesh/library/python/dist/sandesh-0.1dev.tar.gz',
                '/sandesh/common/dist/sandesh-common-0.1dev.tar.gz',]
env.Depends(test_cmd, [env['TOP']+x for x in test_depends])

env.Alias('flaky-test', test_cmd)
env.Alias('controller/src/config/api-server:flaky-test', test_cmd)
