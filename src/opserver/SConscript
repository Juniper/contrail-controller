# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

import sys

Import('BuildEnv')
OpEnv = BuildEnv.Clone()

setup_sources = [
           'setup.py',
           'MANIFEST.in',
          ]

setup_sources_rules = []
for file in setup_sources:
    setup_sources_rules.append(
        OpEnv.Install("", '#controller/src/opserver/' + file))


local_sources = [
           '__init__.py',
           'cpuinfo.py',
           'opserver.py',
           'opserver_client.py',
           'opserver_util.py',
           'sandesh_req_impl.py',
           'uveserver.py',
          ]

local_sources_rules = []
for file in local_sources:
    local_sources_rules.append(OpEnv.Install("opserver", file))
    
viz_sandesh_gen_files = OpEnv.SandeshGenPy('#controller/src/analytics/viz.sandesh')
viz_sandesh_gen_files += OpEnv.SandeshGenPy('#controller/src/analytics/analytics_cpuinfo.sandesh') 
viz_sandesh_gen_files += OpEnv.SandeshGenPy('#controller/src/analytics/redis.sandesh')
viz_sandesh_gen_py_dirs = ['viz', 'analytics_cpuinfo', 'redis']
viz_sandesh_gen_py_files = viz_sandesh_gen_py_dirs + [ '__init__.py' ]
viz_sandesh_gen_py_srcs = ['gen_py/' + src for src in viz_sandesh_gen_py_files]
viz_sandesh_gen_py_rules = []
for file in viz_sandesh_gen_py_srcs:
    viz_sandesh_gen_py_rules.append(OpEnv.Install('opserver/sandesh', file))
OpEnv.Depends(viz_sandesh_gen_py_rules, viz_sandesh_gen_files)

# auto-generate opserver sandesh packages
ops_sandesh_gen_py_files = OpEnv.SandeshGenPy('discovery.sandesh')
ops_sandesh_gen_py_dirs = ['discovery']
ops_sandesh_gen_py_srcs = ['gen_py/' + dir for dir in ops_sandesh_gen_py_dirs]
ops_sandesh_gen_py_rules = []
for file in ops_sandesh_gen_py_srcs:
    ops_sandesh_gen_py_rules.append(OpEnv.Install('opserver/sandesh', file))
OpEnv.Depends(ops_sandesh_gen_py_rules, ops_sandesh_gen_py_files)

# generate the sandesh cpuinfo from base and copy it under analytics_cpuinfo
cpu_info_gen_py_files = OpEnv.SandeshGenPy('#controller/src/base/sandesh/cpuinfo.sandesh')

cpu_info_gen_py_srcs = ['gen_py/cpuinfo']
cpu_info_gen_py_rules = []
for file in cpu_info_gen_py_srcs:
    cpu_info_gen_py_rules.append(
        OpEnv.Install('opserver/sandesh/analytics_cpuinfo', file))
OpEnv.Depends(cpu_info_gen_py_rules, cpu_info_gen_py_files)

doc_sources_rules = []
if sys.platform != 'darwin':
    doc_sources_rules.append(
        OpEnv.Install('.', '#controller/src/docs/opserver'))

sdist_depends = [setup_sources_rules, local_sources_rules, 
                 viz_sandesh_gen_py_rules, doc_sources_rules]

sdist_gen = OpEnv.Command('dist', 'setup.py', 'python setup.py sdist', chdir=1)
# install everything before building distribution
OpEnv.Depends(sdist_gen, sdist_depends)

if 'install' in BUILD_TARGETS:
    install_cmd = OpEnv.Command(None,
                                'setup.py',
                                'python setup.py install %s' %
                                OpEnv['PYTHON_INSTALL_OPT'],
                                chdir = 1)
    OpEnv.Depends(install_cmd, sdist_depends)
    OpEnv.Alias('install', install_cmd)

OpEnv['OPSERVER_PKG'] = sdist_gen
OpEnv.SConscript('test/SConscript', exports='OpEnv', duplicate = 0)

OpEnv.Default(sdist_gen)
OpEnv.Alias("src/opserver:opserver", sdist_gen)

OpEnv.SConscript('node_mgr/SConscript', exports='BuildEnv', duplicate = 0)
OpEnv.Alias('install', OpEnv.Install(
            OpEnv['INSTALL_CONF'], 'contrail-analytics-api.conf'))
