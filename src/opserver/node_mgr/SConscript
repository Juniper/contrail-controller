# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

import sys

Import('BuildEnv')
OpEnv = BuildEnv.Clone()

node_setup_sources = [
           '__init__.py',
           'setup.py',
           'MANIFEST.in',
          ]

node_setup_sources_rules = []
for file in node_setup_sources:
    node_setup_sources_rules.append(
        OpEnv.Install(Dir("."), '#controller/src/opserver/node_mgr/' + file))

# Generate nodemgr python package
analytics_pkg = OpEnv.SandeshGenPy(
	'#controller/src/analytics/analytics.sandesh', "", False)
cpuinfo_pkg = OpEnv.SandeshGenPy('#controller/src/base/sandesh/cpuinfo.sandesh',
                                         'analytics/', False)
process_info_pkg = OpEnv.SandeshGenPy('#controller/src/base/sandesh/process_info.sandesh',
                                         'analytics/', False)
derived_stats_pkg = OpEnv.SandeshGenPy(
        '#tools/sandesh/library/common/derived_stats_results.sandesh',
        'analytics/', False)
OpEnv.Depends(cpuinfo_pkg, analytics_pkg)
OpEnv.Depends(process_info_pkg, analytics_pkg)
OpEnv.Depends(derived_stats_pkg, analytics_pkg)
 
node_sdist_depends = [node_setup_sources_rules, analytics_pkg,
	cpuinfo_pkg, process_info_pkg, derived_stats_pkg]

cd_cmd = 'cd ' + Dir('.').path + ' && '
node_sdist_gen = OpEnv.Command('dist', 'setup.py', cd_cmd + 'python setup.py sdist')
# install everything before building distribution
OpEnv.Depends(node_sdist_gen, node_sdist_depends)

OpEnv.Default(node_sdist_gen)
OpEnv.Alias("opserver:node_mgr", node_sdist_gen)
