#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

# -*- mode: python; -*-

Import('BuildEnv')
import sys

env = BuildEnv.Clone()

env.Append(CPPPATH = [env['TOP'],
                      env['TOP'] + '/io',
                     ])

env.Append(LIBPATH = env['TOP'] + '/base')
env.Append(LIBPATH = env['TOP'] + '/base/test')
env.Append(LIBPATH = env['TOP'] + '/bgp')
env.Append(LIBPATH = env['TOP'] + '/bgp/inet')
env.Append(LIBPATH = env['TOP'] + '/bgp/inetmcast')
env.Append(LIBPATH = env['TOP'] + '/bgp/enet')
env.Append(LIBPATH = env['TOP'] + '/bgp/evpn')
env.Append(LIBPATH = env['TOP'] + '/bgp/test')
env.Append(LIBPATH = env['TOP'] + '/bgp/l3vpn')
env.Append(LIBPATH = env['TOP'] + '/bgp/origin-vn')
env.Append(LIBPATH = env['TOP'] + '/bgp/routing-instance')
env.Append(LIBPATH = env['TOP'] + '/bgp/rtarget')
env.Append(LIBPATH = env['TOP'] + '/bgp/security_group')
env.Append(LIBPATH = env['TOP'] + '/bgp/tunnel_encap')
env.Append(LIBPATH = env['TOP'] + '/control-node')
env.Append(LIBPATH = env['TOP'] + '/db')
env.Append(LIBPATH = env['TOP'] + '/io')
env.Append(LIBPATH = env['TOP'] + '/ifmap')
env.Append(LIBPATH = env['TOP'] + '/net')
env.Append(LIBPATH = env['TOP'] + '/route')
env.Append(LIBPATH = env['TOP'] + '/xmpp')
env.Append(LIBPATH = env['TOP'] + '/xml')
env.Append(LIBPATH = env['TOP'] + '/schema')

env.Prepend(LIBS = [
                    'task_test',
                    'bgptest',
                    'bgp',
                    'peer_sandesh',
                    'control_node',
                    'origin_vn',
                    'routing_instance',
                    'rtarget',                    
                    'security_group',                    
                    'tunnel_encap',                    
                    'ifmap_vnc',
                    'bgp_schema',
                    'sandesh',
                    'http',
                    'http_parser',
                    'curl',
                    'ifmap_server',
                    'ifmap_common',
                    'base',
                    'db',
                    'gunit',
                    'io',
                    'sandeshvns',
                    'net',
                    'route',
                    'xmpp',
                    'bgp_enet',
                    'bgp_evpn',
                    'xmpp_unicast',
                    'xmpp_multicast',
                    'xmpp_enet',
                    'xml',
                    'pugixml',
                    'boost_regex'
                    ])

if sys.platform != 'darwin':
    env.Append(LIBS=['rt'])
    env.Prepend(LINKFLAGS = ['-Wl,--whole-archive',
                             '-lbgp_enet',
                             '-lbgp_evpn',
                             '-lbgp_l3vpn',
                             '-lrtarget',
                             '-ltask_test',
                             '-Wl,--no-whole-archive'])
else:
    lib_enet = Dir('../../enet').path + '/libbgp_enet.a'
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_enet])
    lib_evpn = Dir('../../evpn').path + '/libbgp_evpn.a'
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_evpn])
    lib_l3vpn = Dir('../../l3vpn').path + '/libbgp_l3vpn.a'
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_l3vpn])
    lib_rtarget = Dir('../../rtarget').path + '/librtarget.a'
    env.Prepend(LINKFLAGS = ['-Wl,-force_load,' + lib_rtarget])

env.Append(LIBS = ['bgp_inet', 'bgp_inetmcast'])

inetvpn_ext_peer_test = env.UnitTest('inetvpn_ext_peer_test',
                                 ['inetvpn_ext_peer_test.cc'])
env.Alias('src/bgp/l3vpn:inetvpn_ext_peer_test', inetvpn_ext_peer_test)

inetvpn_peer_test = env.UnitTest('inetvpn_peer_test',
                                 ['inetvpn_peer_test.cc'])
env.Alias('src/bgp/l3vpn:inetvpn_peer_test', inetvpn_peer_test)

inetvpn_route_test = env.UnitTest('inetvpn_route_test',
                                  ['inetvpn_route_test.cc'])
env.Alias('src/bgp/l3vpn:inetvpn_route_test', inetvpn_route_test)

inetvpn_table_test = env.Program('inetvpn_table_test',
                                 ['inetvpn_table_test.cc'])
env.Alias('src/bgp/l3vpn:inetvpn_table_test', inetvpn_table_test)

test_suite = [
    inetvpn_peer_test,
    inetvpn_route_test,
    inetvpn_table_test
]

test = env.TestSuite('bgp-test', test_suite)
env.Alias('controller/src/bgp/l3vpn:test', test)

Return('test_suite')
