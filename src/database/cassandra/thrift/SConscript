#
# Copyright (c) 2015 Juniper Networks, Inc. All rights reserved.
#

Import('DbEnv')
ThriftIfEnv = DbEnv.Clone()

ThriftIfEnv.Append(CPPPATH = ['#/build/include/thrift',
                              ThriftIfEnv['TOP'] + '/database/cassandra/thrift'])

cflags = ThriftIfEnv['CCFLAGS']
cflags.remove('-Werror')
ThriftIfEnv['CCFLAGS'] = cflags

gen_files = ThriftIfEnv.ThriftGenCpp('cassandra.thrift', False)
gen_srcs = ThriftIfEnv.ExtractCpp(gen_files)

local_srcs = ['thrift_if.cc', 'thrift_if_impl.cc']

srcs = gen_srcs + local_srcs
libcassthrift = ThriftIfEnv.Library('cassandra_thrift', srcs)
ThriftIfEnv.Install(ThriftIfEnv['TOP_LIB'], libcassthrift)

ThriftIfEnv.SConscript('test/SConscript', exports='ThriftIfEnv', duplicate=0)
